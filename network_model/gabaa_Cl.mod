TITLE GABAergic conductance with changing Cl- concentration

COMMENT

    Synaptic GABAergic mechanism 
    Two state kinetic scheme synapse described by rise time Trise and decay time constant Tfall. 
    Decay time Tfall MUST be greater than rise time Trise.

    Conductances from each event are added up linearly.
    A normalization factor is evaluated in the INITIAL block such that 
    an event of weight 1 generates a peak conductance of gpeak (uS).

    Reversal potential Egaba is changing according to [Cl-]i change 
    (due to Cl- influx, which we hypothesize to be significant).
    Bicarbonate (HCO3) flows through the GABAR too, and therefore Egaba is also [HCO3]i/[HCO3]o -dependent
    igaba = icl + ihco3 (we assume icl and ihco3 to be mutually independent)
     
    The solution of A->G->bath with rate constants 1/Trise and 1/Tfall is        : %%% TO UNDERSTAND
        A = a*exp(-t/Trise) and
        G = a*Tfall/(Tfall-Trise)*(-exp(-t/Trise) + exp(-t/Tfall))
        where Trise < Tfall

    If Tfall-Trise -> 0 then we have an alphasynapse (proportional to t*exp(-t/T))
    and if Trise -> 0 then we have just single exponential decay.
     
    Because the solution is a sum of exponentials, the coupled equations 
    can be solved as a pair of independent equations
    by the more efficient cnexp method.

    2017-02-20 Adapted from Jedlicka et al 2011
    2017-02-22 Changed name of POINT_PROCESS to gabaaCl
    2017-02-22 Changed celsius to 34 to be consistent with Sohal & Huguenard 2003
    2017-02-22 Renamed tau1 & tau2 as Trise & Tfall
    2017-03-04 Added gpeak and changed the units for g, A, B, total, weight
    2017-03-04 Changed total from GLOBAL to RANGE
    2017-03-05 Moved some variables from PARAMETER to ASSIGNED
    2017-11-01 Made state variables A and B dimensionless
    2017-11-01 Changed Trise from 0.1 -> 0.5 and Tfall from 10 -> 50 -> 75.9
                to be consistent with gabaa.mod 
                (kinetics reflect the measurements of Huntsman et al., 1999)

ENDCOMMENT

NEURON {
    POINT_PROCESS gabaaCl
    USEION cl READ ecl WRITE icl VALENCE -1 
                        : this icl is distributed across the section (mA/cm2)
                        : VALENCE is necessary for calculating ecl
    NONSPECIFIC_CURRENT ihco3
    GLOBAL Trise, Tfall, grel, HCO3e, HCO3i
    RANGE gmax, Ninputs
    GLOBAL factor
    RANGE ehco3, act, total, g, icl, ihco3, i, e, A, B
                                    : this icl is the instantaneous current (nA)
}

UNITS {    
    (mA)    = (milliamp)
    (nA)    = (nanoamp)
    (mV)    = (millivolt)
    (uS)    = (micromho)
    (mM)    = (milli/liter)
    F       = (faraday) (coulombs)
    R       = (k-mole) (joule/degC)
}

PARAMETER {
    : GLOBAL variables whose values are specified in hoc
    Trise   = .5    (ms)    <1e-9, 1e9>    : rising phase time constant (ms) of the GABA-A conductance
    Tfall   = 75.9  (ms)    <1e-9, 1e9>    : falling phase time constant (ms) of the GABA-A conductance
    grel    = 0.18  (1)         : relative conductance of HCO3 at the GABA-A receptor
    HCO3e   = 26    (mM)        : extracellular [HCO3-] (mM), Peter's value (agrees with Jedlicka et al 2011)
    HCO3i   = 16    (mM)        : intracellular [HCO3-] (mM), Jedlicka et al 2011

    : RANGE variables whose values are specified in hoc
    gmax    = 0     (uS)        : maximal conductance (uS) per event at the GABA-A receptor
    Ninputs = 1     (1)         : number of input streams
}

ASSIGNED {
    : Variables that are assigned outside the mod file
    v               (mV)        : postsynaptic membrane potential
    celsius         (degC)      : temperature
    ecl             (mV)        : reversal potential (mV) for Cl-, calculated automatically by NEURON from cli & clo

    : GLOBAL variables that are assigned in the INITIAL block
    factor          (1)         : normalization factor for the conductance

    : RANGE variables that are assigned in the INITIAL block
    ehco3           (mV)        : reversal potential (mV) for HCO3-

    : RANGE variables that are assigned in the NET_RECEIVE block
    act             (1)         : amount of new activation
    total           (1)         : total activation received

    : RANGE variables that are assigned in the BREAKPOINT block
    g               (uS)        : total conductance (uS), split between bicarb (grel*g)
                                : and chloride ((1-grel)*g)
    icl             (nA)        : chloride current (nA) = (1-grel)*g*(v - ecl)
    ihco3           (nA)        : bicarb current (nA) = grel*g*(v - ehco3)
    i               (nA)        : total current (nA) generated by this mechanism = icl + ihco3
    e               (mV)        : reversal potential (mV) for GABA-A receptor
}

STATE {
    A               (1)         : rise variable
	                            :   with maximum value 1 for an isolated IPSC
    B               (1)         : decay variable 
	                            :   with maximum value 1 for an isolated IPSC
                                : B-A yields dual exp timecourse
}

INITIAL { LOCAL tp, mv
    : If Trise is set to be greater or equal to Tfall, set Trise to be a little less than Tfall
    if (Trise/Tfall > .9999) {
        Trise = .9999 * Tfall
    }

    : Calculate normalization factor from Trise & Tfall
    tp = (Trise * Tfall) / (Tfall - Trise) * log(Tfall/Trise)    
                                : t at which exp(-t/Tfall)-exp(-t/Trise) reaches maximum
    mv = exp(-tp/Tfall) - exp(-tp/Trise)    
                                : maximum value of exp(-t/Tfall)-exp(-t/Trise)
    factor = 1/mv               : normalization factor for exp(-t/Tfall)-exp(-t/Trise)

    : Calculate ehco3 from temperature and hco3 concentrations
    ehco3 = log(HCO3i/HCO3e) * (1000) * (celsius + 273.15) * R/F

    : Initialize variables
    A = 0
    B = 0
    total = 0
    icl = 0
    ihco3 = 0
    i = 0
    e = grel * ehco3 + (1-grel) * ecl
}

BREAKPOINT {
    : Calculate rising and falling phases of the total conductance
    SOLVE state METHOD cnexp

    : Calculate the total conductance
    g = (B - A) * factor * gmax

    : Calculate the chloride and bicarbonate currents
    icl = (1-grel) * g * (v-ecl)
    ihco3 = grel * g * (v-ehco3)

    : Calculate the total current through the receptor
    i = icl + ihco3

    : Calculate the reversal potential of the receptor
    e = grel * ehco3 + (1-grel) * ecl
}

DERIVATIVE state {
    A' = -A/Trise
    B' = -B/Tfall
}

NET_RECEIVE(weight (1)) {
    : weight    - amount of activation added by this synaptic event,
    :               relative to the total activation by the simultaneous
    :               activation of all synapses at this site

    : Compute new amount of activation
	act = weight / Ninputs

    : Add activation to current state variables
    A = A + act          : add to the rise variable
    B = B + act          : add to the decay variable

    : Increment the total activation received by this synapse
    total = total + act
}

COMMENT
OLD CODE:

    celsius = 37    (degC)
    g     (uS)        : total conductance, split between bicarb (grel*g)
                : and chloride ((1-grel)*g)
    total    (uS)
    icl = (1-grel) * g * (v-ecl)
    ihco3 = grel * g * (v-ehco3)

    NET_RECEIVE(weight (uS)) {

        GLOBAL total                    : total weight received by all GABA-A receptors

        RANGE Trise, Tfall, HCO3e, HCO3i, grel, gpeak    : %%% TO EXAMINE

        A = A + weight * factor
        B = B + weight * factor
        icl = (1-grel) * g * gpeak * (v-ecl)
        ihco3 = grel * g * gpeak * (v-ehco3)

ENDCOMMENT
